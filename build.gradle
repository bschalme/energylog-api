description = 'API for EnergyLog'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.2.RELEASE")
        classpath('se.transmode.gradle:gradle-docker:1.2')
        classpath('pl.allegro.tech.build:axion-release-plugin:1.3.4')
    }
}

apply plugin: 'docker'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'pl.allegro.tech.build.axion-release'

repositories {
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("com.h2database:h2")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("junit:junit")
}

processResources {
    expand(project.properties)
}

scmVersion {
    tag {
        prefix = project.name
    }
}

project.version = scmVersion.version

String dockerTagVersion = project.version?.endsWith('SNAPSHOT') ? 'latest' : project.version

task buildDocker(type: Docker, dependsOn: build) {
  push = false
  applicationName = jar.baseName
  tagVersion = dockerTagVersion
  dockerfile = file('Dockerfile')
  doFirst {
    copy {
      from jar
      into stageDir
    }
  }
}

task distDocker(type: Docker, dependsOn: build) {
  push = true
  registry = 'registry.internal.airspeed.ca/airspeed'
  applicationName = jar.baseName
  tagVersion = dockerTagVersion
  dockerfile = file('Dockerfile')
  doFirst {
    copy {
      from jar
      into stageDir
    }
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

